plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'me.realized'
version '4.0.0-SirBlobman'
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name 'sirblobman-public'
        url 'https://nexus.sirblobman.xyz/repository/public/'
    }

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name 'oss-sonatype-snapshots'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'placeholderapi'
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'mvdw-software'
        url 'https://repo.mvdw-software.com/content/groups/public/'
    }
}

dependencies {
    // Project Lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    // Spigot API
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

    // Spigot Plugin Dependencies
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        transitive = false
    }

    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.1.1-SNAPSHOT') {
        transitive = false
    }

    compileOnly 'me.clip:placeholderapi:2.10.9'

    // Shaded Dependencies
    implementation 'redis.clients:jedis:3.7.0'
    implementation 'org.apache.commons:commons-pool2:2.11.1'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'org.slf4j:slf4j-nop:2.0.0-alpha5'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    def group = project.group.toString() + '.' + project.name.toLowerCase() + '.shaded.'
    
    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + 'slf4j'
    relocate 'org.bstats', group + 'bstats'
}

build.dependsOn(shadowJar)
